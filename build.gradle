plugins {
    id 'java'
    id 'groovy'
}

group = 'org.example'
version = '0.0.1'

repositories {
    mavenCentral()
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

dependencies {
    // Console UI for the interactive command-line interface
    implementation 'de.codeshelf.consoleui:consoleui:0.0.13'

    // JUnit as test-runner
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    // Spock for test-specification and mocking
    testImplementation 'org.codehaus.groovy:groovy-all:3.0.18'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
}

test {
    useJUnitPlatform()
}

// See: [Creating a Fat Jar in Gradle | Baeldung](https://www.baeldung.com/gradle-fat-jar)
tasks.register('fatJar', Jar) {
    manifest {
        attributes 'Implementation-Title': 'Interactive Anagrams',
                'Implementation-Version': project.version,
                'Main-Class': 'org.example.interactive.ConsoleApp'
    }
    archiveBaseName = 'interactive-anagrams'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}